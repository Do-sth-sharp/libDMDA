cmake_minimum_required (VERSION 3.22)
project (dmda VERSION 1.0.0 LANGUAGES C CXX)

if (NOT DEFINED CMAKE_BUILD_TYPE)
	set (CMAKE_BUILD_TYPE "Release"  CACHE STRING "Choose the type of build." FORCE)
endif (NOT DEFINED CMAKE_BUILD_TYPE)

set (CMAKE_CXX_STANDARD 20)# Using C++20
set (CMAKE_CXX_EXTENSIONS OFF)
set (CMAKE_CXX_STANDARD_REQUIRED ON)
set (CMAKE_C_STANDARD 17)# Using C17
set (CMAKE_C_EXTENSIONS OFF)
set (CMAKE_C_STANDARD_REQUIRED ON)

set (CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>DLL")# Using /MD and /MDd on MSVC
if (MSVC)
	add_compile_definitions ("_CRT_SECURE_NO_WARNINGS")
endif (MSVC)

include (GNUInstallDirs)
include (CMakePackageConfigHelpers)

set (DMDA_INSTALL_NAME ${PROJECT_NAME})

# Targets
add_library (dmda-host STATIC "${CMAKE_CURRENT_SOURCE_DIR}/DMDA.cpp")
add_library (dmda-vst3 STATIC "${CMAKE_CURRENT_SOURCE_DIR}/DMDA.cpp")

target_compile_definitions (dmda-vst3 PUBLIC "DMDA_PLUGIN=1")

target_include_directories (dmda-host PUBLIC
	"$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>"
    "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/${JUCE_INSTALL_NAME}>"
)
target_include_directories (dmda-vst3 PUBLIC
	"$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>"
    "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/${JUCE_INSTALL_NAME}>"
)

# Install
set (DMDA_CMAKE_CONFIG_INSTALL_DIR ${CMAKE_INSTALL_LIBDIR}/cmake/${DMDA_INSTALL_NAME})

install (TARGETS dmda-host
	EXPORT ${DMDA_INSTALL_NAME}Targets
	RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}" OPTIONAL
	LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}" OPTIONAL
	ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}" OPTIONAL
)
install (TARGETS dmda-vst3
	EXPORT ${DMDA_INSTALL_NAME}Targets
	RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}" OPTIONAL
	LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}" OPTIONAL
	ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}" OPTIONAL
)

file (GLOB_RECURSE DMDA_HDR_FILES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
foreach (HDR_FILE ${DMDA_HDR_FILES})
	install (FILES "${CMAKE_CURRENT_SOURCE_DIR}/${HDR_FILE}" DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/${DMDA_INSTALL_NAME}" OPTIONAL)
endforeach (HDR_FILE ${DMDA_HDR_FILES})

write_basic_package_version_file (
	"${CMAKE_CURRENT_BINARY_DIR}/${DMDA_INSTALL_NAME}-config-version.cmake"
	VERSION ${PROJECT_VERSION}
	COMPATIBILITY AnyNewerVersion
)

configure_package_config_file (
	"${CMAKE_CURRENT_LIST_DIR}/${DMDA_INSTALL_NAME}-config.cmake.in"
	"${CMAKE_CURRENT_BINARY_DIR}/${DMDA_INSTALL_NAME}-config.cmake"
	INSTALL_DESTINATION ${DMDA_CMAKE_CONFIG_INSTALL_DIR}
	NO_CHECK_REQUIRED_COMPONENTS_MACRO
)

install (FILES
	"${CMAKE_CURRENT_BINARY_DIR}/${DMDA_INSTALL_NAME}-config.cmake"
	"${CMAKE_CURRENT_BINARY_DIR}/${DMDA_INSTALL_NAME}-config-version.cmake"
	DESTINATION ${DMDA_CMAKE_CONFIG_INSTALL_DIR}
)

install (EXPORT ${DMDA_INSTALL_NAME}Targets
	FILE "${DMDA_INSTALL_NAME}-targets.cmake"
	NAMESPACE ${DMDA_INSTALL_NAME}::
	DESTINATION ${DMDA_CMAKE_CONFIG_INSTALL_DIR}
)
